apply plugin: 'com.android.application'

android {
    compileSdkVersion compileSdk

    defaultConfig {
        applicationId "ru.woesss.j2meloader"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 9
        versionName "b069"
        setProperty("archivesBaseName", "JL-Mod_$versionName")
        ndk {
            abiFilters 'x86', 'armeabi-v7a'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions "default"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    productFlavors {
        play {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "true"
            versionNameSuffix "-play"
            ndk {
                abiFilters 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            }
        }
        open {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            versionNameSuffix "-open"
        }
        fdroid {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
        }
        dev {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
        }
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
}

dependencies {
    def roomVersion = '2.2.5'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'ch.acra:acra-dialog:5.4.0'
    implementation 'com.github.billthefarmer:mididriver:v1.17'
    implementation 'com.github.nikita36078:donations:2.8'
    implementation 'com.github.nikita36078:filepicker:4.3.0'
    implementation 'com.github.yukuku:ambilwarna:2.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'org.ow2.asm:asm:8.0.1'
    implementation project(':dexlib')
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'junit:junit:4.13'
}
